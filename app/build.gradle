plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'application'
	id 'org.liquibase.gradle' version '2.2.0'
	id "com.github.johnrengelman.processes" version "0.5.0"
	id "org.springdoc.openapi-gradle-plugin" version "1.6.0"
	id 'org.siouan.frontend-jdk11' version '6.0.0'
	id 'checkstyle'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

java {
	sourceCompatibility = '20'
}

compileJava {
	options.release = 20
	options.encoding = 'UTF-8'
}

wrapper {
	gradleVersion = '8.3'
	distributionType = Wrapper.DistributionType.ALL
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

def jjwtversion = '0.11.5'

dependencies {

	// Spring Boot dependencies
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.data:spring-data-jpa:3.1.4'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	// Lombok dependencies
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Tests dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// implementation 'org.springframework.security:spring-security-test'

	// Liquibase dependencies and JDBC drivers for H2 and PostgresSQL databases
	liquibaseRuntime 'org.liquibase:liquibase-core:4.16.1'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
	liquibaseRuntime 'info.picocli:picocli:4.6.1'
	implementation 'org.postgresql:postgresql:42.6.0'
	implementation 'com.h2database:h2:2.2.222'

	// Mappers and mapstruct
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// Swagger
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

	// jjwt library for working with JWT
	implementation "io.jsonwebtoken:jjwt-api:${jjwtversion}"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtversion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtversion}"

	// For creating initial data during development
	implementation 'net.datafaker:datafaker:2.0.1'

	implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
}

tasks.named('test') {
	useJUnitPlatform()
}

diffChangelog {
	dependsOn compileJava
}

task stage(dependsOn: [clean, installDist])
installDist.mustRunAfter clean

frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("${projectDir}/frontend").exclude('build', 'node_modules'))
	outputs.dir("${buildDir}/resources/main/static")
}

assembleFrontend {
	configure {configFrontendTasks}

	doLast {
		copy {
			from "${projectDir}/frontend/build"
			into "${buildDir}/resources/main/static"
		}
	}
}

installFrontend {
	configure {configFrontendTasks}
}

processResources.dependsOn(assembleFrontend)
